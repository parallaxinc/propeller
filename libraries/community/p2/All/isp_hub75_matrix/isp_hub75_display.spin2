'' =================================================================================================
''
''   File....... isp_hub75_display.spin2
''   Purpose.... control text placement on HUB75 RGB Matrix
''   Authors.... Stephen M Moraco
''               -- Copyright (c) 2020 Iron Sheep Productions, LLC
''               -- see below for terms of use
''   E-mail..... stephen@ironsheep.biz
''   Started.... Oct 2020
''   Updated.... 01 Dec 2020
''
'' =================================================================================================

CON { Object Interface: PUBLIC ENUMs }

NOT_FOUND = -1

' supported Text Fonts

TEXT_FONT_DEFAULT = fonts.TEXT_FONT_DEFAULT
TEXT_FONT_5x7 = fonts.TEXT_FONT_5x7
TEXT_FONT_8x8A = fonts.TEXT_FONT_8x8A
TEXT_FONT_8x8B = fonts.TEXT_FONT_8x8B

' scrolling support
'  enum: scrolling direction
DIR_UNKNOWN     = scroller.DIR_UNKNOWN
DIR_TO_LEFT     = scroller.DIR_TO_LEFT
DIR_TO_RIGHT    = scroller.DIR_TO_RIGHT

' enum: scrolling duration
SCROLL_UNKNOWN      = scroller.SCROLL_UNKNOWN
SCROLL_FOREVER      = scroller.SCROLL_FOREVER
SCROLL_ONCE         = scroller.SCROLL_ONCE
SCROLL_ONCE_TO_CLEAR = scroller.SCROLL_ONCE_TO_CLEAR
SCROLL_N_TIMES = scroller.SCROLL_N_TIMES

' FIXME: UNDONE make this user configurable
MAX_SCROLLING_REGIONS = 3

DAT { one or more screens of pixels of 24bit color }
' our screen bitmap of RGB colors (pwm bit lengths ;-)
screen0             long    0[screen.DISPLAY_SIZE_IN_LONGS]  ' 6kB buffer (4 bytes / long)

OBJ { Objects Used by this Object }

    screen      : "isp_hub75_screenAccess"
    pixels      : "isp_hub75_screenUtils"
    color       : "isp_hub75_color"
    panelSet    : "isp_hub75_panel"
    fonts       : "isp_hub75_fonts"
    scroller[MAX_SCROLLING_REGIONS] : "isp_hub75_scrollingText"

VAR { Object Instance Variables }
    long    currTextColor
    long    cursorLine
    long    cursorColumn
    long    maxTextLines
    long    maxTextColumns

    long    cog

    byte    selectedTextFont
    byte    topOffsetInPix
    byte    leftOffsetInPix
    byte    charHeightInPix
    byte    charWidthInPix
    byte    horizontalGapInPix
    byte    verticalGapInPix
    byte    hBitmapOffsetInPix
    byte    maxPanelTextLines
    byte    maxPanelTextColumns
    byte    panelFixupOffetInPix

    byte    bScan_1_4
    byte    bSwapRB

PUB start() : ok | scrollerIndex, dvrConfig, driverConfigRaw
    '' Start the underlying driver(s)

    ok := cog := -1 ' mark as failed, initially

    'debug("`term temp size 80 50 textsize 10")
    if screen.MAX_PANELS_PER_COLUMN * screen.MAX_PANELS_PER_ROW <> screen.MAX_PANELS
        debug("dsp:Geometry BAD in isp_hub75_hwGeometry.spin2!")
        abort

    debug("dsp:start()")
    ok := cog := panelSet.start()   ' send buffer to driver
    if ok == -1
        debug("dsp:underlying drivers failed!")
        abort

    debug("dsp: BFFR screen[", udec_long_(screen.DISPLAY_SIZE_IN_LONGS), "] 0x", uhex_long_(@screen0))

    dvrConfig, driverConfigRaw := screen.getDriverFlags()
    bScan_1_4 := (dvrConfig & screen.SCAN_4) > 0 ? True : False
    bSwapRB := (dvrConfig & screen.RB_SWAP) > 0 ? True : False


    ' tell all about location of screen buffer
    screen.setScreen(@screen0)

    ' configure our scrollers for first use
    repeat scrollerIndex from 0 to MAX_SCROLLING_REGIONS - 1
        scroller[scrollerIndex].initialize()
    ' now set text font - which also tell scrollers!
    setTextFont(TEXT_FONT_DEFAULT)

    homeCursor()
    setTextColor($AAAAAA)
    clearScreen()

PUB stop()
    '' Stop the underlying driver(s)
    if cog
        panelSet.stop()
        cog := 0

PUB clearScreen()
    '' Clear the panel to BLACK
    longfill(@screen0, 0, screen.DISPLAY_SIZE_IN_LONGS)
    commitScreenToPanelSet()

PUB clearScreenBuffer()
    '' Clear the panel to BLACK
    longfill(@screen0, 0, screen.DISPLAY_SIZE_IN_LONGS)

PUB fillScreen(rgbColor) | row, column
    '' Fill the display with 24-bit color value
    repeat row from 0 to screen.MAX_DISPLAY_ROWS - 1
        repeat column from 0 to screen.MAX_DISPLAY_COLUMNS - 1
            pixels.drawPixelAtRC(row, column, rgbColor)

DAT
bDidShowOnce long   FALSE

PUB fillPanel(panelIndex, rgbColor) | row, column, columnOffsetToPanel
    '' Fill the specified panel (of panelSet) with 24-bit color value
    columnOffsetToPanel := panelIndex * screen.MAX_PANEL_COLUMNS
    repeat row from 0 to screen.MAX_PANEL_ROWS - 1
        repeat column from 0 to screen.MAX_PANEL_COLUMNS - 1
            'if row == 0 and (column < 4 or column > screen.MAX_PANEL_COLUMNS - 4)
                'if not bDidShowOnce
                '    debug("- fillPanel row=0 col=", udec_(column), ", ", udec(columnOffsetToPanel))
            pixels.drawPixelAtRC(row, column + columnOffsetToPanel, rgbColor)
    'bDidShowOnce := TRUE

PUB fillScreenNoPWM(threeBitColor)
    '' Fill screen but without PWM (for testing underlying driver)
    panelSet.fillScreenNoPWM(threeBitColor)

PUB commitScreenToPanelSet()
    '' Write sceen to panel driver PWM frames (while alternating PWM frame sets)
    'debug("dsp: commit!")

    if bScan_1_4
        panelSet.convertScreen2PWM_14(@screen0, bSwapRB)
        'panelSet.convertScreen2PWM_14_spin(@screen0, bSwapRB)
    else
        panelSet.convertScreen2PWM(@screen0, bSwapRB)


{ --------------     Text Handling     -------------- }

PUB setCursor(line, column)
    '' Position the text cursor to row, column on our display
    ' TODO emit error on bad column/row?
    cursorLine := 0 #> line <# maxTextLines - 1
    cursorColumn := 0 #> column <# maxTextColumns - 1

PUB homeCursor()
    '' Return the cursor to top left corner of our display
    setCursor(0, 0)

PUB setCursorOnPanel(line, column, panelIndex) | boundedPanelIndex, panelOffsetInChars, boundedColumn, tmpCharOffsetInPix, desiredCharOffsetInPix, fixUpInPix
    '' Position the text cursor to row, column of the selected panel
    boundedPanelIndex := 0 #> panelIndex <# screen.MAX_PANELS - 1
    boundedColumn := 0 #> column <# maxPanelTextColumns - 1
    cursorLine := 0 #> line <# maxPanelTextLines - 1
    panelOffsetInChars := (boundedPanelIndex * maxPanelTextColumns) + boundedColumn
    cursorColumn := panelOffsetInChars
    tmpCharOffsetInPix := leftOffsetInPix + (cursorColumn * (charWidthInPix + horizontalGapInPix))
    desiredCharOffsetInPix := leftOffsetInPix + (screen.MAX_PANEL_COLUMNS * boundedPanelIndex) + (boundedColumn * (charWidthInPix + horizontalGapInPix))
    panelFixupOffetInPix := (tmpCharOffsetInPix < desiredCharOffsetInPix) ? desiredCharOffsetInPix - tmpCharOffsetInPix: 0

PRI offsetToTextOnPanel(line, column, panelIndex) : panelLine, panelColumn | gridRow, gridColumn, boundedPanelIndex, boundedColumn, tmpCharOffsetInPix, panelOffsetInChars, desiredCharOffsetInPix
    boundedPanelIndex := 0 #> panelIndex <# screen.MAX_PANELS - 1
    boundedColumn := 0 #> column <# maxPanelTextColumns - 1
    panelLine := 0 #> line <# maxPanelTextLines - 1
    panelOffsetInChars := (boundedPanelIndex * maxPanelTextColumns) + boundedColumn
    panelColumn := panelOffsetInChars
    tmpCharOffsetInPix := leftOffsetInPix + (cursorColumn * (charWidthInPix + horizontalGapInPix))
    desiredCharOffsetInPix := leftOffsetInPix + (screen.MAX_PANEL_COLUMNS * boundedPanelIndex) + (boundedColumn * (charWidthInPix + horizontalGapInPix))
    'panelFixupOffetInPix := (tmpCharOffsetInPix < desiredCharOffsetInPix) ? desiredCharOffsetInPix - tmpCharOffsetInPix: 0

PUB homeCursorOnPanel(panelIndex)
    '' Return the cursor to top left corner of the selected panel
    setCursorOnPanel(0, 0, panelIndex)

PUB setTextFont(newFont) | scrollerIndex, hUnusedPix, vUnusedPix
    '' Select font and update text grid size
    ' currently we only support our 8x8 and 5x7 fonts
    panelFixupOffetInPix := 0
    case newFont
        TEXT_FONT_DEFAULT..TEXT_FONT_5x7:
            charHeightInPix := 7
            charWidthInPix := 5
            hBitmapOffsetInPix := 0
            maxTextLines := (screen.MAX_DISPLAY_ROWS / (charHeightInPix + 1))
            maxTextColumns := (screen.MAX_DISPLAY_COLUMNS / (charWidthInPix + 1))

        TEXT_FONT_8x8A:
            charHeightInPix := 7
            charWidthInPix := 7
            hBitmapOffsetInPix := 1
            maxTextLines := (screen.MAX_DISPLAY_ROWS / (charHeightInPix + 0))  ' 7x7 fonts in 8x8 tile have blank row below so don't add one
            maxTextColumns :=  (screen.MAX_DISPLAY_COLUMNS / (charWidthInPix + 0))  ' 7x7 fonts in 8x8 tile have blank col after so don't add one

        TEXT_FONT_8x8B:
            charHeightInPix := 8
            charWidthInPix := 8
            hBitmapOffsetInPix := 0
            maxTextLines := (screen.MAX_DISPLAY_ROWS / (charHeightInPix + 0))  ' 7x7 fonts in 8x8 tile have blank row below so don't add one
            maxTextColumns :=  (screen.MAX_DISPLAY_COLUMNS / (charWidthInPix + 0))  ' 7x7 fonts in 8x8 tile have blank col after so don't add one

        OTHER:
            debug("*ERROR* unknown text size*")
            abort

    selectedTextFont := newFont

    vUnusedPix := screen.MAX_DISPLAY_ROWS - (maxTextLines * charHeightInPix)
    hUnusedPix := screen.MAX_DISPLAY_COLUMNS - (maxTextColumns * charWidthInPix)
    verticalGapInPix := vUnusedPix / maxTextLines
    horizontalGapInPix := hUnusedPix / maxTextColumns
    vUnusedPix := screen.MAX_DISPLAY_ROWS - (maxTextLines * (charHeightInPix + verticalGapInPix))
    hUnusedPix := screen.MAX_DISPLAY_COLUMNS - (maxTextColumns * (charWidthInPix + horizontalGapInPix))
    topOffsetInPix := (vUnusedPix + 1) >> 1
    leftOffsetInPix := (hUnusedPix + 1) >> 1

    maxPanelTextLines := ((screen.MAX_PANEL_ROWS - topOffsetInPix) / (charHeightInPix + verticalGapInPix))
    maxPanelTextColumns := ((screen.MAX_PANEL_COLUMNS - leftOffsetInPix) / (charWidthInPix + horizontalGapInPix))

    'debug("dsp:TEXT: ", udec(maxTextColumns), udec(maxTextLines))

    'debug("dsp:font ", udec(charHeightInPix), udec(charWidthInPix), udec(maxTextLines), udec(maxTextColumns), udec(verticalGapInPix), udec(horizontalGapInPix), udec(topOffsetInPix), udec(leftOffsetInPix))

    ' configure our scrollers for font choice
    repeat scrollerIndex from 0 to MAX_SCROLLING_REGIONS - 1
        scroller[scrollerIndex].setFontInfo(maxTextColumns, charWidthInPix, charHeightInPix, leftOffsetInPix, horizontalGapInPix, hBitmapOffsetInPix, selectedTextFont)

PUB numberTextLines() : nbrTextLines
    '' Return the number of text-rows for the currently selected font
    nbrTextLines := maxTextLines

PUB numberTextColumns() : nbrTextColumns
    '' Return the number of text-columns for the currently selected font
    nbrTextColumns := maxTextColumns

PUB setTextColor(rgbColor)
    '' Set 24-bit color for subsequent text display
    currTextColor := rgbColor

PUB setTextAtCursor(zString)
    '' Place text 'zero terminated' at our current cursor position using current text color
    setColoredTextAtLnCol(cursorLine, cursorColumn, zString, currTextColor)

PUB setColoredTextAtLnCol(line, column, pString, rgbColor) | charIndex
    '' Place text at specific cursor position using current text color
    setCursor(line, column)
    charIndex := 0
    repeat
        if byte[pString][charIndex] <> 0
            setColorCharAtCursor(byte[pString][charIndex++], rgbColor)
    while byte[pString][charIndex] <> 0

PUB setCharAtCursor(cChar)
    '' Write char to curr cursor position usi g curr text color, increment the cursor position with line wrap
    setColorCharAtCursor(cChar, currTextColor)

PUB setColorCharAtCursor(cChar, rgbColor)
    '' Write char to curr cursor position with given text color, increment the cursor position with line wrap
    setColoredCharAtLnCol(cursorLine, cursorColumn, cChar, rgbColor)
    incrCursorWithWrap()

PUB setCharAtLnCol(line, column, cChar)
    '' Write character to display at R,C using current text color
    setColoredCharAtLnCol(line, column, cChar, currTextColor)

PUB setColoredCharAtLnCol(line, column, cChar, rgbColor) | pCharBitMap, tlScreenOffset, tlCharOffset, cLineIdx, cColumnIdx, sRowPixIdx, sColumnPixIdx, cBitIdx, cByteIdx, cRowBits, pixColumn
    '' Write cChar to display R,C using rgbColor
    'debug("dsp:setCharAtCursor @LnCol(", udec_(line), ",", udec_(column), "), char=", uhex_(cChar), ", RGB=", uhex_long_(rgbColor), udec(topOffsetInPix), udec(leftOffsetInPix))
    cLineIdx := 0 #> line <# maxTextLines - 1
    cColumnIdx :=  0 #> column <# maxTextColumns - 1
    ' get pointer to character bitmap
    pCharBitMap := fonts.getCharAddrForFont(selectedTextFont, cChar)
    'debug("- pChar=", uhex_long(pCharBitMap))
    ' NOTE: cursor is 0-relative so add in our top/left offset to get row/col and mult by char size in pix
    sRowPixIdx := topOffsetInPix + (cLineIdx * (charHeightInPix + verticalGapInPix))
    ' NOTE: panelFixupOffetInPix is only non-zero when we are placing text on specific panel!
    sColumnPixIdx := leftOffsetInPix + panelFixupOffetInPix + (cColumnIdx * (charWidthInPix + horizontalGapInPix))


    repeat cByteIdx from 0 to charHeightInPix - 1
        cRowBits := byte[pCharBitMap][cByteIdx]
        'debug("- char row(", udec_(cByteIdx), ") ", ubin(cRowBits))
        repeat cBitIdx from charWidthInPix - 1 to 0
            pixColumn := sColumnPixIdx + cBitIdx
            if (cRowBits & (1 << ((charWidthInPix - 1) - cBitIdx))) > 0
                pixels.drawPixelAtRC(sRowPixIdx + cByteIdx, pixColumn, rgbColor)
            'else
            '    pixels.drawPixelAtRC(sRowPixIdx + cByteIdx, pixColumn, $ff0000)

{ --------------     Scrolling Text     -------------- }
' EXAMPLES
'    configureScrollLoopMode(scrollerID, display.SCROLL_ONCE)
'    configureScrollLoopCount(scrollerID, display.SCROLL_N_TIMES, 2)

PUB configureScrollLoopMode(indexAllocated, loopMode)
    '' Specify scrolling text duration (forever, once, twice)
    'debug("dsp:configureScrollLoopMode() ", udec(indexAllocated))
    if indexAllocated >= 0 && indexAllocated < MAX_SCROLLING_REGIONS
        scroller[indexAllocated].configureScrollLoopMode(loopMode)

PUB configureScrollLoopCount(indexAllocated, loopMode, loopCount)
    '' Specify scrolling text duration (forever, once, twice)
    'debug("dsp:configureScrollLoopCount() ", udec(indexAllocated))
    if indexAllocated >= 0 && indexAllocated < MAX_SCROLLING_REGIONS
        scroller[indexAllocated].configureScrollLoopCount(loopMode, loopCount)


PUB scrollTextOnLine(line, pZString, direction) : indexAllocated
    '' Place scolling text on line N, of pZString scrolling in direction using current text color
    indexAllocated := scrollColoredTextOnLn(line, pZString, direction, currTextColor)


PUB scrollColoredTextOnLn(line, pZString, direction, rgbColor) : indexAllocated  | cLineIdx, cColumnIdx, widthInChars, sRowPixIdx, sColumnPixIdx
    '' Place scolling text on line N [0 - N-1], of pZString scrolling in direction using rgbColor
    'debug("dsp:scrollColoredTextOnLn()")
    ' ensure we are asked to use a good location
    cLineIdx := 0 #> line <# maxTextLines - 1
    cColumnIdx :=  0
    ' convert line info to row,col
    ' NOTE: cursor is 0-relative so add in our top/left offset to get row/col and mult by char size in pix
    widthInChars := maxTextColumns
    sRowPixIdx := topOffsetInPix + (cLineIdx * (charHeightInPix + verticalGapInPix))
    sColumnPixIdx := leftOffsetInPix + (cColumnIdx * (charWidthInPix + horizontalGapInPix))

    indexAllocated := scrollTextAtRCOfColor(sRowPixIdx, sColumnPixIdx, widthInChars, pZString, direction, rgbColor)

PUB scrollTextOnLnOfNPanels(line, panelIndex, panelCount, pZString, direction) : indexAllocated
    '' Place scolling text on line N, of pZString scrolling in direction using current text color
    indexAllocated := scrollColoredTextOnLnOfNPanels(line, panelIndex, panelCount, pZString, direction, currTextColor)

PUB scrollColoredTextOnLnOfNPanels(line, panelIndex, panelCount, pZString, direction, rgbColor) : indexAllocated | cLineIdx, cColumnIdx, widthInChars, sRowPixIdx, sColumnPixIdx, panelLine, panelColumn, boundedPanelIdx, cPanelCt, offsetPixelRows, offsetPixelColumns
    '' Place scolling text on line N [0 - N-1], of pZString scrolling in direction using rgbColor
    'debug("dsp:scrollColoredTextOnLnOfNPanels()")
    boundedPanelIdx := 0 #> panelIndex <# screen.MAX_PANELS - 1
    cPanelCt := 1 #> panelCount <# screen.MAX_PANELS
    cLineIdx := 0 #> line <# maxTextLines - 1
    cColumnIdx :=  0
    widthInChars := ((cPanelCt * screen.MAX_PANEL_COLUMNS) - (2 * leftOffsetInPix)) / (charWidthInPix + horizontalGapInPix)
    'panelLine, panelColumn := offsetToTextOnPanel(cLineIdx, cColumnIdx, panelIndex)
    offsetPixelRows, offsetPixelColumns := screen.offsetToPanel(panelIndex)
    ' ensure we are asked to use a good location
    ' convert line info to row,col
    ' NOTE: cursor is 0-relative so add in our top/left offset to get row/col and mult by char size in pix
    sRowPixIdx := topOffsetInPix + (cLineIdx * (charHeightInPix + verticalGapInPix))
    sColumnPixIdx := offsetPixelColumns + leftOffsetInPix
    indexAllocated := scrollTextAtRCOfColor(sRowPixIdx, sColumnPixIdx, widthInChars, pZString, direction, rgbColor)

PUB scrollTextAtRC(row, column, widthInChars, pZString, direction) : indexAllocated
    '' Place scolling text at absolute R,C, of pZString scrolling in direction using current text color
    indexAllocated := scrollTextAtRCOfColor(row, column, widthInChars, pZString, direction, currTextColor)


PUB scrollTextAtRCOfColor(row, column, widthInChars, pZString, direction, rgbColor) : indexAllocated
    '' Place scolling text at absolute R,C, of pZString scrolling in direction using rgbColor
    indexAllocated := nextFreeScroller()
    'debug("dsp:scrollTextAtRCOfColor() ", udec(indexAllocated))
    scroller[indexAllocated].scrollTextAtRCOfColor(row, column, widthInChars, pZString, direction, rgbColor)


PUB isScrollingComplete() : status | scrollerIndex
    '' Return T/F where T means that all movement for screolling text regions is complete
    status := TRUE
    ' if one or more scrollers are still moving we are not done!
    repeat scrollerIndex from 0 to MAX_SCROLLING_REGIONS - 1
        if NOT scroller[scrollerIndex].isScrollingComplete()
            status := FALSE
            quit    ' outta here we have our answer

PUB animateScroll() | scrollerIndex
    '' Do one scroll step, if one is pending
    repeat scrollerIndex from 0 to MAX_SCROLLING_REGIONS - 1
        if NOT scroller[scrollerIndex].isScrollingComplete()
            scroller[scrollerIndex].animateScroll()

PRI nextFreeScroller() : scrollerIndexToUse | scrollerIndex
    scrollerIndexToUse := NOT_FOUND
    repeat scrollerIndex from 0 to MAX_SCROLLING_REGIONS - 1
        if scroller[scrollerIndex].isIdle()
            scrollerIndexToUse := scrollerIndex
            quit
    if scrollerIndexToUse == NOT_FOUND
        debug("Failed to locate free scroller!")
        abort
    'debug("dsp:nextFreeScroller() ", udec(scrollerIndexToUse))

{ --------------     Basic Graphics     -------------- }

PUB drawBox(topRow, leftColumn, width, height, filled)
    '' Draw box outline/filled of width x height at R,C (using current text color)
    drawBoxOfColor(topRow, leftColumn, width, height, filled, currTextColor)

PUB drawBoxOfColor(topRow, leftColumn, width, height, filled, rgbColor) | rightColumn, bottomRow, rowIndex
    '' Draw box outline/filled of width x height at R,C using rgbColor
    rightColumn := leftColumn + width - 1
    bottomRow := topRow + height - 1
    if filled == TRUE
        repeat rowIndex from 0 to height - 1
            drawLineOfColor(topRow + rowIndex, leftColumn, topRow + rowIndex, rightColumn, rgbColor)    ' horiz row
    else
        drawLineOfColor(topRow, leftColumn, topRow, rightColumn, rgbColor)    ' horiz top
        drawLineOfColor(topRow, rightColumn, bottomRow, rightColumn, rgbColor)    ' vert right
        drawLineOfColor(bottomRow, leftColumn, bottomRow, rightColumn, rgbColor)    ' horiz bottom
        drawLineOfColor(topRow, leftColumn, bottomRow, leftColumn, rgbColor)    ' vert left

PUB drawPanelBox(panelIndex, topRow, leftColumn, width, height, filled)
    '' Draw in desired panel a box outline/filled of width x height at R,C (using current text color)
    drawPanelBoxOfColor(panelIndex, topRow, leftColumn, width, height, filled, currTextColor)

PUB drawPanelBoxOfColor(panelIndex, topRow, leftColumn, width, height, filled, rgbColor) | rightColumn, bottomRow, rowIndex, offsetPixelRows, offsetPixelColumns
    '' Draw in desired panel a box outline/filled of width x height at R,C using rgbColor
    offsetPixelRows, offsetPixelColumns := screen.offsetToPanel(panelIndex)
    topRow :=  0 #> topRow <# screen.MAX_PANEL_ROWS - 1
    leftColumn :=  0 #> leftColumn <# screen.MAX_PANEL_COLUMNS - 1
    height :=  0 #> height <# screen.MAX_PANEL_ROWS - 1
    width :=  0 #> width <# screen.MAX_PANEL_COLUMNS - 1
    rightColumn := leftColumn + width - 1
    bottomRow := topRow + height - 1
    ' offset everything to desired panel
    topRow += offsetPixelRows
    bottomRow += offsetPixelRows
    leftColumn += offsetPixelColumns
    rightColumn += offsetPixelColumns
    if filled == TRUE
        repeat rowIndex from 0 to height - 1
            drawLineOfColor(topRow + rowIndex, leftColumn, topRow + rowIndex, rightColumn, rgbColor)    ' horiz row
    else
        drawLineOfColor(topRow, leftColumn, topRow, rightColumn, rgbColor)    ' horiz top
        drawLineOfColor(topRow, rightColumn, bottomRow, rightColumn, rgbColor)    ' vert right
        drawLineOfColor(bottomRow, leftColumn, bottomRow, rightColumn, rgbColor)    ' horiz bottom
        drawLineOfColor(topRow, leftColumn, bottomRow, leftColumn, rgbColor)    ' vert left

PUB drawLine(fmRow, fmColumn, toRow, toColumn)
    '' Draw line fromRC -> toRC using current text color (currently limited to horzontal/vertical lines)
    drawLineOfColor(fmRow, fmColumn, toRow, toColumn, currTextColor)

PUB drawLineOfColor(fmRow, fmColumn, toRow, toColumn, rgbColor) | row, column, dx, dy, ctr, incr, intD
    '' Draw line fromRC -> toRC using rgbColor (currently limited to horzontal/vertical lines)
    fmRow :=  0 #> fmRow <# screen.MAX_DISPLAY_ROWS - 1
    fmColumn :=  0 #> fmColumn <# screen.MAX_DISPLAY_COLUMNS - 1
    toRow :=  0 #> toRow <# screen.MAX_DISPLAY_ROWS - 1
    toColumn :=  0 #> toColumn <# screen.MAX_DISPLAY_COLUMNS - 1
    'debug("seg:drwLn fmRC=(", udec_(fmRow), ",", udec_(fmColumn), "), toRC=(", udec_(toRow), ",", udec_(toColumn), "), RGB=(", uhex_long(rgbColor), ")")
    if fmRow == toRow
        ' draw Horizontal Line
        repeat column from fmColumn to toColumn
            pixels.drawPixelAtRC(fmRow, column, rgbColor)
    elseif fmColumn == toColumn
        ' draw Vertical Line
        repeat row from fmRow to toRow
            pixels.drawPixelAtRC(row, fmColumn, rgbColor)
    else
        if abs(toRow - fmRow) < abs(toColumn - fmColumn)
            if fmColumn > toColumn
                plotLineLow(toColumn, toRow, fmColumn, fmRow, rgbColor)
            else
                plotLineLow(fmColumn, fmRow, toColumn, toRow, rgbColor)
        else
            if fmRow > toRow
                plotLineHigh(toColumn, toRow, fmColumn, fmRow, rgbColor)
            else
                plotLineHigh(fmColumn, fmRow, toColumn, toRow, rgbColor)

PUB drawPanelLine(panelIndex, fmRow, fmColumn, toRow, toColumn)
    '' Draw line fromRC -> toRC using current text color (currently limited to horzontal/vertical lines)
    drawPanelLineOfColor(panelIndex, fmRow, fmColumn, toRow, toColumn, currTextColor)

PUB drawPanelLineOfColor(panelIndex, fmRow, fmColumn, toRow, toColumn, rgbColor) | row, column, dx, dy, ctr, incr, intD, desiredPanelIndex, offsetPixelRows, offsetPixelColumns
    '' Draw line fromRC -> toRC using rgbColor (currently limited to horzontal/vertical lines)
    fmRow :=  0 #> fmRow <# screen.MAX_PANEL_ROWS - 1
    fmColumn :=  0 #> fmColumn <# screen.MAX_PANEL_COLUMNS - 1
    toRow :=  0 #> toRow <# screen.MAX_PANEL_ROWS - 1
    toColumn :=  0 #> toColumn <# screen.MAX_PANEL_COLUMNS - 1
    desiredPanelIndex :=  0 #> panelIndex <# screen.MAX_PANELS - 1
    offsetPixelRows, offsetPixelColumns := screen.offsetToPanel(panelIndex)
    'debug("seg:drwLn fmRC=(", udec_(fmRow), ",", udec_(fmColumn), "), toRC=(", udec_(toRow), ",", udec_(toColumn), "), RGB=(", uhex_long(rgbColor), ")")
    drawLineOfColor(fmRow + offsetPixelRows, fmColumn + offsetPixelColumns, toRow + offsetPixelRows, toColumn + offsetPixelColumns, rgbColor)

PRI plotLineLow(x0, y0, x1, y1, rgbColor) | row, column, dy, dx, D, yi
    dx := x1 - x0
    dy := y1 - y0
    yi := 1
    if dy < 0
        yi := -1
        dy := -dy
    D := (2 * dy) - dx
    row := y0

    repeat column from x0 to x1
        pixels.drawPixelAtRC(row, column, rgbColor)
        if D > 0
            row := row + yi
            D := D + (2 * (dy - dx))
        else
            D := D + 2 * dy

PRI plotLineHigh(x0, y0, x1, y1, rgbColor) | row, column, dy, dx, D, xi
    dx := x1 - x0
    dy := y1 - y0
    xi := 1
    if dx < 0
        xi := -1
        dx := -dx
    D := (2 * dx) - dy
    column := x0

    repeat row from y0 to y1
        pixels.drawPixelAtRC(row, column, rgbColor)
        if D > 0
            column := column + xi
            D := D + (2 * (dx - dy))
        else
            D := D + 2*dx

{ --------------     Misc Helpers     -------------- }

PRI incrCursorWithWrap() | priorRow, priorCol
    ' increment cursor position, wrapping at end of line when needed
    priorRow := cursorLine
    priorCol := cursorColumn
    cursorColumn++
    if cursorColumn > maxTextColumns - 1
        cursorColumn := 0
        cursorLine++
        if cursorLine > maxTextLines - 1
            cursorLine := 0
    'debug("incr curPOS RC(", udec_(priorRow), ",", udec_(priorCol), ") -> RC(", udec_(cursorLine), ",", udec_(cursorColumn), ")")

PRI isDebugLocn(nRow, nCol) : status
    status := FALSE
    if (nRow == 31 && nCol < 4) || (nRow < 2 && nCol < 3) || (nRow == 31 && nCol > 61) || (nRow < 2 && nCol > 61)
    'if (nRow  > 29)
        status := TRUE ' FALSE  ' turn off debug


CON { license }

{{


 -------------------------------------------------------------------------------------------------
  MIT License

  Copyright (c) 2020 Iron Sheep Productions, LLC

  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in all
  copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.
 =================================================================================================

}}
